--------------------------------------------------------------------------------
-- This module demonstrates the similitudes between various semantics for STLC
-- before giving a generic notion of Scope-and-Type Safe Semantics Ã  la
-- Type-and-scope Safe Programs and Their Proofs
-- (Allais, Chapman, McBride, and McKinna, CPP 17)
--------------------------------------------------------------------------------

module StateOfTheArt.STLC where

open import indexed
open import var hiding (_<$>_ ; get)
open import environment as E hiding (_>>_ ; extend)
open import StateOfTheArt.Types

open import Data.Nat.Base
open import Data.List.Base hiding ([_] ; _++_ ; lookup)
open import Function

--------------------------------------------------------------------------------
-- Well scoped-and-typed Simply-Typed Lambda Calculus

data Lam : Type â”€Scoped where
  V : {Ïƒ : Type} â†’    [ Var Ïƒ                âŸ¶ Lam Ïƒ        ]
  A : {Ïƒ Ï„ : Type} â†’  [ Lam (Ïƒ â‡’ Ï„) âŸ¶ Lam Ïƒ  âŸ¶ Lam Ï„        ]
  L : {Ïƒ Ï„ : Type} â†’  [ (Ïƒ âˆ·_) âŠ¢ Lam Ï„       âŸ¶ Lam (Ïƒ â‡’ Ï„)  ]

--------------------------------------------------------------------------------
-- A Generic Notion of Semantics and the corresponding generic traversal

record Sem (ğ“¥ ğ“’ : Type â”€Scoped) : Set where
  field  th^ğ“¥  : âˆ€ {Ïƒ} â†’ Thinnable (ğ“¥ Ïƒ)
         âŸ¦VâŸ§   : {Ïƒ : Type} â†’               [ ğ“¥ Ïƒ               âŸ¶ ğ“’ Ïƒ        ]
         âŸ¦AâŸ§   : {Ïƒ Ï„ : Type} â†’             [ ğ“’ (Ïƒ â‡’ Ï„) âŸ¶ ğ“’ Ïƒ   âŸ¶ ğ“’ Ï„        ]
         âŸ¦LâŸ§   : (Ïƒ : Type) â†’ {Ï„ : Type} â†’  [ â–¡ (ğ“¥ Ïƒ âŸ¶ ğ“’ Ï„)     âŸ¶ ğ“’ (Ïƒ â‡’ Ï„)  ]


  sem : {Î“ Î” : List Type} {Ïƒ : Type} â†’ (Î“ â”€Env) ğ“¥ Î” â†’ (Lam Ïƒ Î“ â†’ ğ“’ Ïƒ Î”)
  sem Ï (V k)    = âŸ¦VâŸ§ (lookup Ï k)
  sem Ï (A f t)  = âŸ¦AâŸ§ (sem Ï f) (sem Ï t)
  sem Ï (L b)    = âŸ¦LâŸ§ _ (Î» Ïƒ v â†’ sem (extend Ïƒ Ï v) b) where

   extend : {Î“ Î” Î˜ : List Type} {Ïƒ : Type} â†’
            Thinning Î” Î˜ â†’ (Î“ â”€Env) ğ“¥ Î” â†’ ğ“¥ Ïƒ Î˜ â†’ (Ïƒ âˆ· Î“ â”€Env) ğ“¥ Î˜
   extend Ïƒ Ï v = (Î» t â†’ th^ğ“¥ t Ïƒ) <$> Ï âˆ™ v
open Sem

--------------------------------------------------------------------------------
-- Defining various traversals as instances of Sem

Renaming : Sem Var Lam
Renaming = record
  { th^ğ“¥  = th^Var
  ; âŸ¦VâŸ§    = V
  ; âŸ¦AâŸ§    = A
  ; âŸ¦LâŸ§    = Î» Ïƒ b â†’ L (b (pack s) z) }

rename : âˆ€ {Î“ Î” Ïƒ} â†’ (Î“ â”€Env) Var Î” â†’ Lam Ïƒ Î“ â†’ Lam Ïƒ Î”
rename = Sem.sem Renaming

Substitution : Sem Lam Lam
Substitution = record
   { th^ğ“¥  = Î» t Ï â†’ Sem.sem Renaming Ï t
   ; âŸ¦VâŸ§    = id
   ; âŸ¦AâŸ§    = A
   ; âŸ¦LâŸ§    = Î» Ïƒ b â†’ L (b (pack s) (V z)) }

Subst : Context â†’ Context â†’ Set
Subst Î“ Î” = (Î“ â”€Env) Lam Î”

subst : âˆ€ {Î“ Î” Ïƒ} â†’ (Î“ â”€Env) Lam Î” â†’ Lam Ïƒ Î“ â†’ Lam Ïƒ Î”
subst = Sem.sem Substitution

--------------------------
-- Single substitution

_[_] : âˆ€ {Ïƒ Ï„} â†’ [ (Ïƒ âˆ·_) âŠ¢ Lam Ï„ âŸ¶ Lam Ïƒ âŸ¶ Lam Ï„ ]
_[_] {Ïƒ} {_} {Î“} N M =  subst Ï N
  where
  Ï : Subst (Ïƒ âˆ· Î“) Î“
  lookup Ï z      =  M
  lookup Ï (s x)  =  V x

